#!/usr/bin/env -S csi -ss

;; This script preprocesses text from stdin for use as a TIPP10 typing tutor
;; lesson.

(import (chicken io)
	(chicken irregex)
	(clojurian syntax)
	srfi-1
	srfi-14
	utf8)

(define max-lines 400)
(define min-char-variety 5)
(define replacements '(("[\t ]+" . " ")
		       ("^ " . "")
		       (" $" . "")))

(define (replace s)
  (fold (lambda (pair s)
	  (let ((pattern (car pair))
		(insert (cdr pair)))
	    (irregex-replace/all pattern s insert)))
	s replacements))

(define (varied s)
  (>= (length (char-set->list (string->char-set s)))
      min-char-variety))

(define (take* n lst)
  (take lst n))

(define (main args)
  (->> (read-lines)
       (map replace)
       (filter varied)
       (take* max-lines)
       (for-each print)))
