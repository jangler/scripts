#!/usr/bin/env -S csi -ss

;; This script preprocesses text from stdin for use as a TIPP10 typing tutor
;; lesson.

(import (chicken io)
	(chicken irregex)
	(clojurian syntax)
	srfi-14
	srfi-127
	utf8)

(define max-lines 400)
(define min-char-variety 5)
(define replacements '(("[\t ]+" . " ")
		       ("^ " . "")
		       (" $" . "")))

(define (replace s)
  (foldl (lambda (s pair)
	   (let ((pattern (car pair))
		 (insert (cdr pair)))
	     (irregex-replace/all pattern s insert)))
	 s replacements))

(define (varied s)
  (>= (length (char-set->list (string->char-set s)))
      min-char-variety))

(define (lseq-take* i lst)
  (lseq-take lst i))

(define (main args)
  (->> (generator->lseq read-line)
       (lseq-map replace)
       (lseq-filter varied)
       (lseq-take* max-lines)
       (lseq-for-each print)))
