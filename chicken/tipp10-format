#!/usr/bin/env -S csi -ss

;; This script preprocesses text from stdin for use as a TIPP10 typing tutor
;; lesson.

(import (chicken io)
	(chicken irregex)
	srfi-14
	srfi-158
	srfi-197
	utf8)

(define max-lines 400)
(define min-char-variety 5)
(define replacements '(("[\t ]+" . " ")
		       ("^ " . "")
		       (" $" . "")))

(define (replace s)
  (foldl (lambda (s pair)
	   (let ((pattern (car pair))
		 (insert (cdr pair)))
	     (irregex-replace/all pattern s insert)))
	 s replacements))

(define (varied s)
  (>= (length (char-set->list (string->char-set s)))
      min-char-variety))

(define (main args)
  (chain read-line
	 (gmap replace _)
	 (gfilter varied _)
	 (gtake _ max-lines)
	 (generator-for-each print _)))
